Shader "PerikShaders/FlatShading"
{
    Properties
    {
        _BaseColor ("Base Color", Color) = (1,1,1,1)
        _BaseTex ("Base Texture", 2D) = "white" {}
    }
    SubShader
    {
        Tags
        {
            "RenderType"="Opaque"
            "Queue" = "Geometry"
            "RenderPipeline" = "UniversalPipeline"
        }
        LOD 100

        Pass
        {
            Tags
            {
                "LightMode" = "UniversalForward"
            }
            
            HLSLPROGRAM
            #pragma vertex vert
            #pragma fragment frag

            #include "Packages/com.unity.render-pipelines.universal/ShaderLibrary/Core.hlsl"
            #include "Packages/com.unity.render-pipelines.universal/ShaderLibrary/Lighting.hlsl"
                        
            struct appdata
            {
                float4 positionOS : Position;
                float2 uv : TEXCOORD0;
                float3 normalOS : NORMAL;
            };

            struct v2f
            {
                float4 positionCS : SV_Position;
                float2 uv : TEXCOORD0;
                nointerpolation float4 flatLighting : TEXCOORD1;
            };
            
            sampler2D _BaseTex;

            CBUFFER_START(UnityPerMaterial)
            float4 _BaseColor;
            float4 _BaseTex_ST;
            CBUFFER_END

            v2f vert (appdata v)
            {
                v2f o;
                o.positionCS = TransformObjectToHClip(v.positionOS.xyz);
                o.uv = TRANSFORM_TEX(v.uv, _BaseTex);
                float3 normalWS = TransformObjectToWorldNormal(v.normalOS);
                float3 ambient = SampleSHVertex(normalWS);
                Light mainLight = GetMainLight();
                float3 diffuse = mainLight.color * max(0, dot(normalWS, mainLight.direction));
                o.flatLighting = float4(ambient + diffuse, 1.0f);
                return o;
            }

            float4 frag (v2f i) : SV_TARGET
            {
                float4 textureSample = tex2D(_BaseTex, i.uv);
                return textureSample * _BaseColor * i.flatLighting;
            }
            
            ENDHLSL
        }
    }
}
